Program ::= Expr

Expr    ::= Const | Var
          | Diff | Minus | Add | Mul | Div
          | Zero | Equal | Greater | Less
          | Cons | Car | Cdr | Null | Empty
          | If | Let | Unpack

Const   ::= Number
Var     ::= Id

Diff    ::= '-' '(' Expr ',' Expr ')'
Minus   ::= 'minus' '(' Expr ')'
Add     ::= 'add' '(' Expr ',' Expr ')'
Mul     ::= 'mul' '(' Expr ',' Expr ')'
Div     ::= 'div' '(' Expr ',' Expr ')'

Zero    ::= 'zero?' '(' Expr ')'
Equal   ::= 'equal?' '(' Expr ',' Expr ')'
Greater ::= 'greater?' '(' Expr ',' Expr ')'
Less    ::= 'less?' '(' Expr ',' Expr ')'

Cons    ::= 'cons' '(' Expr ',' Expr ')'
Car     ::= 'car' '(' Expr ')'
Cdr     ::= 'cdr' '(' Expr ')'
Null    ::= 'null?' '(' Expr ')'
Empty   ::= 'emptylist'

If      ::= 'if' Expr 'then' Expr 'else' Expr
Let     ::= 'let' Id '=' Expr 'in' Expr
Unpack  ::= 'unpack' (Identifier)* '=' Expr 'in' Expr

Number  ::= [0-9]+
Id      ::= [a-z]+

/* Use https://www.bottlecaps.de/rr/ui to view it as a Railroad Diagram. */
